buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        jcenter()
    }
    dependencies {
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
        classpath "com.badlogicgames.gdx:gdx-tools:1.9.6"
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    version = '1.0'
    ext {
        appName = "little-prince"
        gdxVersion = '1.9.6'
        roboVMVersion = '2.3.0'
        box2DLightsVersion = '1.4'
        ashleyVersion = '1.7.0'
        aiVersion = '1.8.0'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

project(":desktop") {
    apply plugin: "java"


    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-controllers-desktop:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-controllers-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    }
}

project(":html") {
    apply plugin: "gwt"
    apply plugin: "war"


    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx:$gdxVersion:sources"
        compile "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion:sources"
        compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion:sources"
        compile "com.badlogicgames.gdx:gdx-box2d-gwt:$gdxVersion:sources"
        compile "com.badlogicgames.gdx:gdx-controllers:$gdxVersion:sources"
        compile "com.badlogicgames.gdx:gdx-controllers-gwt:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-controllers-gwt:$gdxVersion:sources"
        compile "com.badlogicgames.box2dlights:box2dlights:$box2DLightsVersion:sources"
        compile "net.dermetfan.libgdx-utils:libgdx-utils:0.13.4:sources"
        compile "net.dermetfan.libgdx-utils:libgdx-utils-box2d:0.13.4:sources"
        compile "net.dermetfan.libgdx-utils:libgdx-utils:0.13.4:sources"
    }
}

project(":core") {
    apply plugin: "java"


    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-controllers:$gdxVersion"
        compile "com.badlogicgames.box2dlights:box2dlights:$box2DLightsVersion"
        compile "net.dermetfan.libgdx-utils:libgdx-utils-box2d:0.13.4"
        compile "net.dermetfan.libgdx-utils:libgdx-utils:0.13.4"
        compile group: 'org.ejml', name: 'all', version: '0.30'
    }
}

tasks.eclipse.doLast {
    delete ".project"
}

// Store the parameters you want to pass the texturePacker here...
project.ext.walkAssets = [ "core/assets/walk_src/", "core/assets/walk/", "walk" ]
project.ext.baobabAssets = [ "core/assets/baobab_src/", "core/assets/baobab/", "baobab" ]

// Import the texture packer
import com.badlogic.gdx.tools.texturepacker.TexturePacker

// Add a new task that packs the textures for you
task texturePacker << {
    if (project.ext.has('walkAssets')) {
        logger.info "Calling TexturePacker: "+walkAssets
        TexturePacker.process(walkAssets[0], walkAssets[1], walkAssets[2])
    }
    if (project.ext.has('baobabAssets')) {
        logger.info "Calling TexturePacker: "+baobabAssets
        TexturePacker.process(baobabAssets[0], baobabAssets[1], baobabAssets[2])
    }
}
